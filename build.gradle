plugins {
   id 'us.ihmc.gradle.ihmc-build' version '0.0.9'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.1'

project.ext.fullVersion = version
project.ext.vcsUrl = "https://github.com/ihmcrobotics/ihmcsoemwrapper"
project.ext.licenseURL = "http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
project.ext.licenseName = "Apache License, Version 2.0"
project.ext.bintrayLicenseName = "Apache-2.0"


sourceSets {
	main {
		java {
			srcDirs += 'src/'
		}
	}
}

repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
  maven {
      url  "http://dl.bintray.com/ihmcrobotics/maven-release"
  }
}

dependencies {
    compile group: 'us.ihmc', name: 'SOEM', version: '1.3.1'
	compile group: 'org.javolution', name: 'javolution-core-java', version: '6.0.0'
    
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",

      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": project.ext.licenseURL,
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc'
			artifactId project.name
			version "$version"
			from components.java

			pom.withXml {
                            asNode().children().last() + {
                                resolveStrategy = Closure.DELEGATE_FIRST
                                name project.name
                                url project.ext.vcsUrl
                                licenses {
                                    license {
                                        name project.ext.licenseName
                                        url project.ext.licenseURL
                                        distribution 'repo'
                                    }
                                }
                                developers {
                                    developer {
                                        id 'jsmith'
                                        name 'Jesper Smith'
                                        email 'jsmith@ihmc.us'
                                    }
                                }
                            }

                            }


			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}

def labels = ['ihmc', 'robotics', 'java', 'embedded', 'ethercat']
ihmc.configureForIHMCOpenSourceBintrayPublish(false, "mavenJava", "maven-release", labels)
